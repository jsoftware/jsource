prolog './g100p.ijs'
NB. +/\ B ---------------------------------------------------------------

(0 0 1 1 ,: 0 1 1 2) -: +/\ 0 0 1 1 ,: 0 1 0 1
(>:i.20) -: +/\20$1

plus=: 4 : 'x+y'

(+/\   -: plus/\  ) x=:?    13$2
(+/\   -: plus/\  ) x=:?7   13$2
(+/\"1 -: plus/\"1) x
(+/\   -: plus/\  ) x=:?3 5 13$2
(+/\"1 -: plus/\"1) x
(+/\"2 -: plus/\"2) x
(+/\   -: plus/\  ) x=:?    12$2
(+/\   -: plus/\  ) x=:?4   12$2
(+/\"1 -: plus/\"1) x
(+/\   -: plus/\  ) x=:?4 8 12$2
(+/\"1 -: plus/\"1) x
(+/\"2 -: plus/\"2) x
(2 0 2 $ 0.5) (-: , -:&(3!:0)) 3 %/\ 0 1{~  i. 4 0 2


NB. +/\ I ---------------------------------------------------------------

plus=: 4 : 'x+y'

(+/\ -: plus/\) x=:1 2 3 1e9 2e9
(+/\ -: plus/\) |.x

(+/\   -: plus/\  ) x=:_1e4+?    13$2e4
(+/\   -: plus/\  ) x=:_1e4+?4   13$2e4
(+/\"1 -: plus/\"1) x
(+/\   -: plus/\  ) x=:_1e4+?3 5 13$2e4
(+/\"1 -: plus/\"1) x
(+/\"2 -: plus/\"2) x

(+/\   -: plus/\  ) x=:_1e9+?    13$2e9
(+/\   -: plus/\  ) x=:_1e9+?4   13$2e9
(+/\"1 -: plus/\"1) x
(+/\   -: plus/\  ) x=:_1e9+?3 5 13$2e9
(+/\"1 -: plus/\"1) x
(+/\"2 -: plus/\"2) x
(2 0 2 $ 0.5) (-: , -:&(3!:0)) 3 %/\ 1 + i. 4 0 2


NB. +/\ D ---------------------------------------------------------------

plus=: 4 : 'x+y'

1e_12 > >./ | , (+/\   - plus/\  ) x=:0.01*_1e4+?    13$2e4
1e_12 > >./ | , (+/\   - plus/\  ) x=:0.01*_1e4+?4   13$2e4
1e_12 > >./ | , (+/\"1 - plus/\"1) x
1e_12 > >./ | , (+/\   - plus/\  ) x=:0.01*_1e4+?3 5 13$2e4
1e_12 > >./ | , (+/\"1 - plus/\"1) x
1e_12 > >./ | , (+/\"2 - plus/\"2) x
(2 0 2 $ 0.5) (-: , -:&(3!:0)) 3 %/\ 1.5 + i. 4 0 2


NB. +/\. Z ---------------------------------------------------------------

plus=: 4 : 'x+y'

1e_12 > >./ | , (+/\   - plus/\  ) x=:j./0.1*_1e2+?2     13$2e2
1e_12 > >./ | , (+/\   - plus/\  ) x=:j./0.1*_1e2+?2 4   13$2e2
1e_12 > >./ | , (+/\"1 - plus/\"1) x
1e_12 > >./ | , (+/\   - plus/\  ) x=:j./0.1*_1e2+?2 3 5 13$2e2
1e_12 > >./ | , (+/\"1 - plus/\"1) x
1e_12 > >./ | , (+/\"2 - plus/\"2) x
(2 0 2 $ 1j1) (-: , -:&(3!:0)) 3 %/\ 1j1 + i. 4 0 2

(,'j')    -: +/\'j'
(,<'ace') -: +/\<'ace'
(,'j')    -: +/\u:'j'
(,<'ace') -: +/\<u:'ace'
(,'j')    -: +/\10&u:'j'
(,<'ace') -: +/\<10&u:'ace'
(,s:@<"0 'j')    -: +/\s:@<"0 'j'
(,s:@<"0&.> <'ace') -: +/\s:@<"0&.> <'ace'
(,<"0@s: <'ace') -: +/\<"0@s: <'ace'

'domain error' -: +/\ etx 'deipnosophist'
'domain error' -: +/\ etx ;:'peace in our time'
'domain error' -: +/\ etx u:'deipnosophist'
'domain error' -: +/\ etx u:&.> ;:'peace in our time'
'domain error' -: +/\ etx 10&u:'deipnosophist'
'domain error' -: +/\ etx 10&u:&.> ;:'peace in our time'
'domain error' -: +/\ etx s:@<"0 'deipnosophist'
'domain error' -: +/\ etx s:@<"0&.> ;:'peace in our time'
'domain error' -: +/\ etx <"0@s: ;:'peace in our time'

4!:55 ;:'f plus x'



epilog''

