NB. d. scalar functions -------------------------------------------------

equ=: 2 : '''x'' -:&(5!:5)&< ''y'''
eqt=: 2 : '(x t. i.20) -: (y t. i.20)'
eqv=: 2 : '(x v) -: y v=. 0.01*_500+?2 3$1000'

1:            d. 1 eqt 0:
2.5"0         d. 1 eqt 0:
_14"0         d. 1 eqt 0:
-~            d. 1 eqt 0:
%~            d. 1 eqt 0:
6&p.          d. 1 eqt 0:
_6&p.         d. 1 eqt 0:
''&p.         d. 1 eqt 0:

>:            d. 1 eqt 1:
<:            d. 1 eqt 1:
]             d. 1 eqt 1:
[             d. 1 eqt 1:
3&+           d. 1 eqt 1:
+&3           d. 1 eqt 1:
-&3           d. 1 eqt 1:
2.5&+         d. 1 eqt 1:
+&2.5         d. 1 eqt 1:

-             d. 1 eqt _1:
-.            d. 1 eqt _1:
2.5&-         d. 1 eqt _1:
_1&*          d. 1 eqt _1:
*&_1          d. 1 eqt _1:

+:            d. 1 eqt 2:
+~            d. 1 eqt 2:
-:            d. 1 eqt (0.5"0)
o.            d. 1 eqt (1p1"0)

*:            d. 1 equ +:
^.            d. 1 equ %
^             d. 1 equ ^
%:            d. 1 equ (-:@%@%:)
0&o.          d. 1 equ (- % 0&o.)
1&o.          d. 1 equ (2&o.)
2&o.          d. 1 equ (-@(1&o.))
2&!           d. 1 eqt (_0.5 1&p.)
2&!           d. 1 eqt (-:@(] * <:) d. 1)

(+:   + *:  ) d. 1 eqt (2: + +:  )
(+:   + 1:  ) d. 1 eqt 2:
(^.   + 1&o.) d. 1 equ (%  + 2&o.)

(+:   - *:  ) d. 1 eqt (2: - +:  )
(+:   - 1:  ) d. 1 eqt 2:
(1:   - +:  ) d. 1 eqt _2:
(17"0 - +:  ) d. 1 eqt _2:
(^.   - 1&o.) d. 1 equ (%  - 2&o.)

(]    * <:  ) d. 1 eqt (+:@(2&!)    d. 1)     
(]    * >:  ) d. 1 eqt (+:@(2&!)@>: d. 1)
(1&o. * ^   ) d. 1 eqv ((2&o.*^) + (1&o.*^))   
(1&o. * ]   ) d. 1 eqv ((2&o.*]) + 1&o.) 

(1&o. % ]   ) d. 1 eqv (((2&o.*]) - 1&o.) % *:)

(]    ^ 0:  ) d. 1 eqt 0:
(]    ^ 1:  ) d. 1 eqt 1:
(]    ^ 2:  ) d. 1 eqt +:
(]    ^ 3:  ) d. 1 eqt (0 0 3&p.)
(]    ^ 4:  ) d. 1 eqt (0 0 0 4&p.)
(]    ^ 5:  ) d. 1 eqt (0 0 0 0 5&p.)
(]    ^ 2j5"0)d. 1 eqv (2j5&* @ (^&1j5))
(]    ^ _1: ) d. 1 eqv (% d. 1)

^@(]^2:)      d. 1 eqv (^@*: d. 1)
*:@^          d. 1 eqt (+:@^ * ^)
2&!@*:        d. 1 eqt ((2&! d. 1)@*: * +:)
(1&o.@+:)     d. 1 eqt (+:@(2&o.)@+:)
(+:@(1&o.))   d. 1 eqt (+:@(2&o.))
(*:@>:)       d. 1 eqt (2 2&p.)
(>:@*:)       d. 1 eqt (0 2&p.)

0&o.          d. 1 equ (- % 0&o.)
0&o.          d. 1 eqv (%:@-.@*: d. 1)

*:^:3         d. 1 eqt (8: * ^&7  )
*:^:2         d. 1 eqt (*:@*: d. 1)
*:^:1         d. 1 eqt (*:    d. 1)
*:@>:^:3      d. 1 eqt ((p.. *:@>:@*:@>:@*:@>: t. i.12)&p.)
>:@*:^:3      d. 1 eqt (>:@*:@>:@*:@>:@*: d. 1)
+:^:_1        d. 1 eqt (0.5"_     )
+:^:_2        d. 1 eqt (0.25"_    )

^&3                 d. _1 eqt ((^&2 * ]) d. _1)
^&3                 d. _1 eqt ((*: *  ]) d. _1)

1 2 3&p.            d. _1 eqt ((1: + +: + 3&*@*:)  d. _1)
(1 2 3&p. + 4 5&p.) d. _1 eqt (5 7 3&p.            d. _1)
(1 2 3&p. - 4 5&p.) d. _1 eqt (_3 _3 3&p.          d. _1)
(1 2 3&p. * 4 5&p.) d. _1 eqt ((+//.1 2 3*/4 5)&p. d. _1)

((1 % *:) d.  1) equ ((1: % *:) d.  1) 
((1 + *:) d. _1) equ ((1: + *:) d. _1)


4!:55 ;:'eqt equ eqv'


